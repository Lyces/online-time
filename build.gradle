plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.3'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'
    compileOnly 'de.tr7zw:item-nbt-api:2.10.0'

    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

bukkit {
    main = 'com.nextplugins.onlinetime.NextOnlineTime'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    load = 'POSTWORLD'
    depend = ['NBTAPI']
    softDepend = ['PlaceholderAPI', 'Citizens', 'HolographicDisplays']

    commands {
        tempo {
            aliases = ['tempoonline']
        }
    }
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.onlinetime.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.onlinetime.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.onlinetime.libs.hikari'

    relocate 'de.tr7zw.annotations', 'com.nextplugins.onlinetime.libs.annotations'

}

compileJava {
    options.encoding = "UTF-8"

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
